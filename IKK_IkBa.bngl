begin model
begin parameters
    #volumes
    vc  2.7e-12 #volume cell L
    vcm = vc/1000 #volume cell m^3
    nc  3.3 #cytoplasm to nucleus ratio
    V_nuc  vc/(nc+1) #L
    V_nucm = V_nuc/1000 #volume nucleus m^3
    V_cyt  V_nuc*nc
    V_ext 5e-12 # made up because it does not matter
    pi = 3.14
    r_cell = (vcm/((4*pi)/3))^(1/3) #m
    r_nuc = (V_nucm/((4*pi)/3))^(1/3) #m
    SA_plm = 4*pi*r_cell #m^2
    SA_num = 4*pi*r_nuc #m^2
    V_plm = (SA_plm*10e-9)*1000 # L SA in m^2 x 10 nm thickness times 1000 (m^3 to L)
    V_num = (SA_num*10e-9)*1000 # L
    # IKK parameters
    ka  0.0004 #s^-1 IKK activation rate
    ki  0.003 #s^-1 IKK inactivation rate
    kp  0.0006 #s^-1 IKK inact to neutral rate
    kc1a 0.074 #s^-1 IKK phos of free IkBa rate
    kc2a 0.370 #s^-1 IKK phos of NFkB bound IkBa rate
    kt1a 0.1 #s^-1 IKK phos degradation rate
    # IkBa NFkB complex formation
    ka1a 5e4 # M/s IkBa NFkB association same in nuc and cyt
    kd1a 0.05 #s^-1 IkBa NFkB dissacociation same in nuc and cyt
    #transport
    ki1 0.0026 #s^-1 NFkB nuclear import
    ke1 0.00052 #s^-1 NFkB nuclear export
    ke2a 0.01 #s^-1 IkBa-NfkB nuclear export
    ki3a 0.00067 #s^-1 IkBa nuclear import
    ke3a 0.000335 #s^-1 IkBa nuclear export
    #IkBa synthesis and degradation
    c1a 1.4*1e-1 # M*s^-1 tIkBa synthesis
    c2a 0.5 #s^-1 IkBa synthesis
    c3a 0.0003 #s^-1 tIkBa degradation
    c4a 0.0005 #s^-1 IkBa degradation
    c5a 0.000022 # IkBa degradation off IkBa-NFkB complex
    k1 = (1/0.065)
    h1 = 2
    #a20 synthesis and degradation
    c1 2*1e-1 # M*s^-1 tA20 synthesis rate
    c2 0.5 #s^-1 protein synthesis
    c3 0.0004 #s^-1 transcript degradation
    c4 0.0045 #s^-1 protein degradation
    c6a 0.00004 #s^-1 tComp degradation
    h2 = 3
    N_avo  6.02214e23
    conv  N_avo*1e-15 #conc. (M) to molecule number conversion volume in um^3
    #starting concentrations
    IKK_0 0.08e-6*N_avo*V_cyt #molar x N_avo x volume = molecules/cytoplasm
    IkBaNFkB_0 0.10e-6*N_avo*V_cyt
    gene_0 2
end parameters

begin molecule types
    IKK(s~n~a~i)
    IkBa(nf,y~o~p)
    NFkB(ikb)
    tIkBa
    geneIkBa
    A20
    tA20
    geneA20
    Target
    tTarget
    geneTarget
    Comp
    tComp
    geneComp
end molecule types

begin compartments
    Ext 3 V_ext #extracell space
    Plm 2 V_plm Ext #plasma membrane
    Cyt 3 V_cyt Plm #cytoplasm
    Num 2 V_num Cyt #nuclear membrane
    Nuc 3 V_nuc Num #nucleus
end compartments

begin observables
    Molecules freeCytNFkB NFkB(ikb)@Cyt
    Molecules freeCytIkBa IkBa(nf,y~o)@Cyt
    Species cytIkBaNFkB @Cyt:IkBa(nf!0,y~o).NFkB(ikb!0)
    Molecules freeNucNFkB NFkB(ikb)@Nuc
    Molecules freeNucIkBa IkBa(nf,y~o)@Nuc
    Species nucIkBaNFkB @Nuc:IkBa(nf!0,y~o).NFkB(ikb!0)
    Molecules tIkBa tIkBa
    Molecules neutralIKK IKK(s~n)
    Molecules activeIKK IKK(s~a)
    Species IKKIkBa IKK(s~a).IkBa(nf,y~o)
    Species IKKIkBaNFkB IKK(s~a).IkBa(nf!0,y~o).NFkB(ikb!0)
    Molecules inactiveIKK IKK(s~i)
    Molecules tA20 tA20
    Molecules A20 A20
    Molecules CytpIkBa IkBa(nf,y~p)@Cyt
    Species CytpIkBaNFkB @Cyt:IkBa(nf!0,y~p).NFkB(ikb!0)
    Molecules tTarget tTarget
    Molecules tComp tComp
    Molecules Comp Comp
end observables

begin seed species
    IKK(s~n)@Cyt  IKK_0
    @Cyt:IkBa(nf!0,y~o).NFkB(ikb!0) IkBaNFkB_0
    geneIkBa@Nuc gene_0
    geneA20@Nuc gene_0
    geneTarget@Nuc gene_0
    geneComp@Nuc gene_0
end seed species

begin reaction rules
    # IKK rules
    IKK(s~n) -> IKK(s~a)  ka
    IKK(s~a) -> IKK(s~i)  ki
    IKK(s~i) -> IKK(s~n)  kp
    IKK(s~a) + IkBa(nf,y~o) -> IKK(s~a) + IkBa(nf,y~p) kc1a/N_avo
    IKK(s~a) + IkBa(nf!0,y~o).NFkB(ikb!0) -> IKK(s~a) +\
     IkBa(nf!0,y~p).NFkB(ikb!0) kc2a/N_avo
    IkBa(nf,y~p) -> 0 kt1a
    # IkbA NFkB complex
    IkBa(nf,y~o) + NFkB(ikb) -> IkBa(nf!0,y~o).NFkB(ikb!0) ka1a/N_avo #nuc and cyt
    IkBa(nf!0,y~o).NFkB(ikb!0) -> IkBa(nf,y~o) + NFkB(ikb) kd1a #nuc and cyt
    # transport
    NFkB(ikb)@Cyt -> NFkB(ikb)@Nuc ki1
    NFkB(ikb)@Nuc -> NFkB(ikb)@Cyt ke1
    @Nuc:IkBa(nf!0,y~o).NFkB(ikb!0) -> @Cyt:IkBa(nf!0,y~o).NFkB(ikb!0) ke2a
    IkBa(nf,y~o)@Cyt -> IkBa(nf,y~o)@Nuc ki3a
    IkBa(nf,y~o)@Nuc -> IkBa(nf,y~o)@Cyt ke3a
    # IkbA  synthesis and degradation
    NFkB(ikb)@Nuc + geneIkBa -> NFkB(ikb)@Nuc + geneIkBa + tIkBa Hill(c1a,k1,h1)
    tIkBa -> tIkBa + IkBa(nf,y~o)@Cyt c2a
    tIkBa -> 0 c3a
    IkBa(nf,y~o)@Cyt -> 0 c4a
    IkBa(nf!0,y~o).NFkB(ikb!0)@Cyt -> NFkB(ikb)@Cyt c5a
    # A20 synthesis and degradation
    NFkB(ikb)@Nuc + geneA20 -> NFkB(ikb)@Nuc + geneA20 + tA20 Hill(c1,k1,h2)
    tA20 -> tA20 + A20 c2
    tA20 -> 0 c3
    A20 -> 0 c4
    #Prototypical inducible target transcript
    NFkB(ikb)@Nuc + geneTarget -> NFkB(ikb)@Nuc + geneTarget + tTarget Hill(c1,k1,h2)
    tTarget -> 0 c3
    # competitor synthesis and degradation
    #NFkB(ikb)@Nuc + Comp -> NFkB(ikb)@Nuc + Comp + tComp c1a/N_avo
    #tComp -> tComp + Comp c2a
    #tComp -> 0 c6a
    #Comp -> 0 c4a
end reaction rules
end model

## actions ##
begin actions
    generate_network({overwrite=>1})

    # Equilibration..
    setParameter("ka",0)
    simulate({method=>"ode",t_end=>240000,n_steps=>4001,sparse=>1,steady_state=>1})
    saveConcentrations()
    # activate pathway
    setParameter("ka",0.001)
    simulate({method=>"ode",t_end=>36000,n_steps=>600,atol=>1e-8,rtol=>1e-8,sparse=>1, suffix=>stim})
end actions
